apply plugin: 'com.android.application'

// Begin of reading the sensitive data
// GV 21/02/2018
def noKeyFound = '"NO_KEY_FOUND"'
def nextCloudPassword = noKeyFound
def userName = noKeyFound

def sensitiveDataFile = file('../sensitive_data.txt')
def Properties sensitiveData = new Properties()

if (sensitiveDataFile.canRead()){
    // Read the sensitive data from file sensitive_data.txt
    sensitiveData.load(new FileInputStream(sensitiveDataFile))

    if (sensitiveData != null) {
        if (sensitiveData.containsKey('UserName')) {
            userName = sensitiveData['UserName']
        }
        if (sensitiveData.containsKey('NextCloudPassword')) {
            nextCloudPassword = sensitiveData['NextCloudPassword']
        }
    }
}

// In this section a reviewer can insert directly his own
// sensitive data by replacing the strings beginning with REVIEWERS_
// Just double click on the string and paste you own data
if (userName == noKeyFound) {
    userName = '"REVIEWERS_USER_NAME_GOES_HERE"'
}
if (nextCloudPassword == noKeyFound) {
    nextCloudPassword = '"REVIEWERS_NEXTCLOUD_PASSWORD_GOES_HERE"'
}

// End of reading the sensitive data


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.hendrik.mianamalaga"
        minSdkVersion 16
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // add a Constant Field for sensitive_data
        buildConfigField "String", "NEXT_CLOUD_PASSWORD", nextCloudPassword
        // add a Resource String for the same key-value pair
        resValue "string", "NEXT_CLOUD_PASSWORD", nextCloudPassword
        // add a Constant Field for sensitive_data
        buildConfigField "String", "NEXT_CLOUD_USERNAME", userName
        // add a Resource String for the same key-value pair
        resValue "string", "NEXT_CLOUD_USERNAME", userName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets { main { java.srcDirs = ['src/main/java', 'src/main/java/Activities'] } }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:support-v13:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.google.code.gson:gson:2.8.4'
    implementation 'com.dropbox.core:dropbox-core-sdk:3.0.10'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.github.nextcloud:android-library:-SNAPSHOT'
}
